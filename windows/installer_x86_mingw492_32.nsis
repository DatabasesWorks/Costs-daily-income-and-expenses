; ===== Defines =====
!define /date DATE "%Y%m%d"
!define APPLICATION_NAME "Costs"
; !define APPLICATION_VERSION "0.0.2-${DATE}"
!define PROJECT_ROOT ".\"
!define QT_BINARY_DIR "E:\Qt\5.5\mingw492_32\bin"
!define QT_PLUGINS_DIR "E:\Qt\5.5\mingw492_32\plugins"

!include "FileFunc.nsh"
!insertmacro GetTime

!define MUI_ICON "..\costs.ico"
!define MUI_UNICON "..\costs.ico"

CRCCheck On

!ifndef QT_BINARY_DIR
	!error "QT_BINARY_DIR is not defined"
!endif

!ifndef QT_PLUGINS_DIR
	!error "QT_PLUGINS_DIR is not defined"
!endif

!getdllversion "${PROJECT_ROOT}\tmp\release\Costs.exe" expv_
!define APPLICATION_VERSION "${expv_1}.${expv_2}.${expv_3}.${expv_4}-${DATE}"

; ===== Includes =====
!include "MUI.nsh"

; ===== General =====
Name "${APPLICATION_NAME} ${APPLICATION_VERSION}"
OutFile "${APPLICATION_NAME}-${APPLICATION_VERSION}-x86-mingw492_32-installer.exe"
InstallDir "$PROGRAMFILES\${APPLICATION_NAME}"
SetCompressor "lzma"

Function finishpageaction
CreateShortcut "$desktop\Costs.lnk" "$instdir\Costs.exe"
FunctionEnd

; ===== Pages =====
!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_LICENSE "..\LICENSE"
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES
!define MUI_FINISHPAGE_NOAUTOCLOSE
!define MUI_FINISHPAGE_RUN
!define MUI_FINISHPAGE_RUN_CHECKED
!define MUI_FINISHPAGE_RUN_TEXT "Run Application"
!define MUI_FINISHPAGE_RUN_FUNCTION "RunApplication"
!define MUI_FINISHPAGE_SHOWREADME ""
!define MUI_FINISHPAGE_SHOWREADME_NOTCHECKED
!define MUI_FINISHPAGE_SHOWREADME_TEXT "Create Desktop Shortcut"
!define MUI_FINISHPAGE_SHOWREADME_FUNCTION finishpageaction
!insertmacro MUI_PAGE_FINISH

!insertmacro MUI_UNPAGE_WELCOME
!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES
!insertmacro MUI_UNPAGE_FINISH

!insertmacro MUI_LANGUAGE "English"

; ===== Installer =====
Section ""
	SetOutPath $INSTDIR
	
	CreateDirectory $INSTDIR\platforms
	CreateDirectory $INSTDIR\sqldrivers
	
	File ${PROJECT_ROOT}\tmp\release\Costs.exe
	File ${QT_BINARY_DIR}\libgcc_s_dw2-1.dll
	File ${QT_BINARY_DIR}\libstdc++-6.dll
	File ${QT_BINARY_DIR}\libwinpthread-1.dll
	
	File ${QT_BINARY_DIR}\Qt5Widgets.dll
	File ${QT_BINARY_DIR}\Qt5Gui.dll
	File ${QT_BINARY_DIR}\Qt5Core.dll
	File ${QT_BINARY_DIR}\Qt5Sql.dll
	
	SetOutPath $INSTDIR\platforms
	File ${QT_PLUGINS_DIR}\platforms\qminimal.dll
	File ${QT_PLUGINS_DIR}\platforms\qwindows.dll
	
	SetOutPath $INSTDIR\sqldrivers
	File ${QT_PLUGINS_DIR}\sqldrivers\qsqlite.dll
	
	SetOutPath $INSTDIR
	WriteUninstaller $INSTDIR\uninstall.exe
	
	;Create start menu entry
	createDirectory "$SMPROGRAMS\${APPLICATION_NAME}"
	createShortCut "$SMPROGRAMS\${APPLICATION_NAME}\${APPLICATION_NAME}.lnk" "$instdir\Costs.exe"
	
	;Write uninstall information to the registry
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${APPLICATION_NAME}" "DisplayName" "${APPLICATION_NAME} (remove only)"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${APPLICATION_NAME}" "UninstallString" "$INSTDIR\Uninstall.exe"
	
SectionEnd

; ===== Uninstaller =====
Section "Uninstall"	
	Delete $INSTDIR\LICENSE
	Delete $INSTDIR\*.dll
	Delete $INSTDIR\platforms\*.dll
	Delete $INSTDIR\plugins\sqldrivers\*.dll
	Delete $INSTDIR\Costs.exe
	Delete $INSTDIR\uninstall.exe
	RMDir $INSTDIR\platforms
	RMDir $INSTDIR\plugins\sqldrivers
	RMDir $INSTDIR\plugins
	RMDir $INSTDIR
	
	;Delete reg entries
	DeleteRegKey HKEY_LOCAL_MACHINE "SOFTWARE\${APPLICATION_NAME}"
	DeleteRegKey HKEY_LOCAL_MACHINE "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\${APPLICATION_NAME}"
	
	;Delete start menu shortcut
	Delete "$DESKTOP\Costs.lnk"	
	
	;Remove start menu lnk
	delete "$SMPROGRAMS\${APPLICATION_NAME}\${APPLICATION_NAME}.lnk"
	;Try to remove the start menu dir
	rmDir "$SMPROGRAMS\${APPLICATION_NAME}"
SectionEnd

; ===== Functions =====
Function RunApplication
	Exec '"$WINDIR\explorer.exe" "$INSTDIR\Costs.exe"'
FunctionEnd